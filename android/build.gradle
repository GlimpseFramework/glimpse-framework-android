description = 'Glimpse Framework Android Implementation'

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'

android {
	compileSdkVersion 23
	buildToolsVersion "23.0.1"

	defaultConfig {
		minSdkVersion 18
		targetSdkVersion 23
		versionCode 2
		versionName version
	}

	buildTypes {
		debug {
			debuggable true
		}
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			debuggable false
		}
	}

	repositories {
		jcenter()
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
	}

	lintOptions {
		abortOnError false
		disable 'AllowBackup'
	}
}

dokka {
	moduleName = 'Glimpse Framework Android Implementation'
	outputFormat = 'javadoc'
}

def releaseDir = "${buildDir}/release/${version}"

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

task dokkaJar(type: Jar, dependsOn: project.dokka) {
	classifier = 'javadoc'
	from dokka
}

artifacts {
	archives sourcesJar
	archives dokkaJar
}

uploadArchives {
	repositories.mavenDeployer {
		pom.groupId = 'org.glimpseframework'
		pom.artifactId = 'glimpse-framework-android'
		pom.version = "${glimpse_version}"

		repository(url: "file://${releaseDir}")
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])

	compile "org.glimpseframework:glimpse-framework-api:${glimpse_version}"
	compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_version}"
	compile 'org.jetbrains.anko:anko-sdk23:0.9'

	testCompile "io.kotlintest:kotlintest:${kotlintest_version}"
}
